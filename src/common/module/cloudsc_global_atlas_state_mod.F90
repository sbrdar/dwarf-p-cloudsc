! (C) Copyright 1988- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE CLOUDSC_GLOBAL_ATLAS_STATE_MOD
  ! Driver module to manage the setup and teardown of the dwarf memory state
  USE PARKIND1,  ONLY : JPIM, JPRB
  USE YOMPHYDER, ONLY : STATE_TYPE
  USE YOECLDP,   ONLY : NCLV, YRECLDP, YRECLDP_LOAD_PARAMETERS
  USE YOMCST,    ONLY : YOMCST_LOAD_PARAMETERS
  USE YOETHF,    ONLY : YOETHF_LOAD_PARAMETERS
  USE YOEPHLI  , ONLY : YREPHLI, YREPHLI_LOAD_PARAMETERS

  USE FILE_IO_MOD, ONLY: INPUT_INITIALIZE, INPUT_FINALIZE, LOAD_SCALAR, LOAD_ARRAY
  USE EXPAND_MOD, ONLY: LOAD_AND_EXPAND, LOAD_AND_EXPAND_STATE, &
      & LOADVAR_ATLAS, LOADSTATE_ATLAS
  USE VALIDATE_MOD, ONLY: VALIDATE
  USE CLOUDSC_MPI_MOD, ONLY: IRANK

  USE ATLAS_MODULE
  USE, INTRINSIC :: ISO_C_BINDING
  USE ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS_MODULE

  IMPLICIT NONE

  TYPE(ATLAS_STRUCTUREDGRID) :: GRID
  TYPE(ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS) :: FSPACE
  TYPE(ATLAS_FIELDSET) :: FSET

  TYPE VAR3D_PTR
      REAL(C_DOUBLE), POINTER :: PTR(:,:,:)
  END TYPE

  ! note: the last six variables have special types
  CHARACTER(LEN=10), PARAMETER, DIMENSION(30) :: VAR_NAMES = (/ &
      "PLCRIT_AER", "PICRIT_AER", "PRE_ICE   ", "PCCN      ", "PNICE     ", "PT        ", "PQ        ", &
      "PVFA      ", "PVFL      ", "PVFI      ", "PDYNA     ", "PDYNL     ", "PDYNI     ", "PHRSW     ", &
      "PHRLW     ", "PVERVEL   ", "PAP       ", "PLU       ", "PLUDE     ", "PSNDE     ", "PMFU      ", &
      "PMFD      ", "PA        ", "PSUPSAT   ", &
      "PLSM      ", "LDCUM     ", "KTYPE     ", "PAPH      ", "PEXTRA    ", "PCLV      " /)

  TYPE CLOUDSC_GLOBAL_ATLAS_STATE
    ! Memory state containing raw fields annd tendencies for CLOUDSC dwarf
    !
    ! Note that the global state has an additional outermost block
    ! dimension allocated for each field variable.
    INTEGER(KIND=JPIM)                   :: NPROMA, KLEV    ! Grid points and vertical levels per block
    INTEGER(KIND=JPIM)                   :: NGPTOT, NBLOCKS ! Total number of grid points and blocks
    INTEGER(KIND=JPIM)                   :: KFLDX 
    LOGICAL                              :: LDSLPHY 
    LOGICAL                              :: LDMAINCALL      ! T if main call to cloudsc
    REAL(KIND=JPRB)                      :: PTSPHY          ! Physics timestep

    TYPE(VAR3D_PTR), DIMENSION(24) :: VARS_3D_REAL64

    ! Input field variables and tendencies
    REAL(c_double), POINTER :: PLCRIT_AER(:,:,:)
    REAL(c_double), POINTER :: PICRIT_AER(:,:,:) 
    REAL(c_double), POINTER :: PRE_ICE(:,:,:) 
    REAL(c_double), POINTER :: PCCN(:,:,:)     ! liquid cloud condensation nuclei
    REAL(c_double), POINTER :: PNICE(:,:,:)    ! ice number concentration (cf. CCN)
    REAL(c_double), POINTER :: PT(:,:,:)       ! T at start of callpar
    REAL(c_double), POINTER :: PQ(:,:,:)       ! Q at start of callpar
    REAL(c_double), POINTER :: PVFA(:,:,:)     ! CC from VDF scheme
    REAL(c_double), POINTER :: PVFL(:,:,:)     ! Liq from VDF scheme
    REAL(c_double), POINTER :: PVFI(:,:,:)     ! Ice from VDF scheme
    REAL(c_double), POINTER :: PDYNA(:,:,:)    ! CC from Dynamics
    REAL(c_double), POINTER :: PDYNL(:,:,:)    ! Liq from Dynamics
    REAL(c_double), POINTER :: PDYNI(:,:,:)    ! Liq from Dynamics
    REAL(c_double), POINTER :: PHRSW(:,:,:)    ! Short-wave heating rate
    REAL(c_double), POINTER :: PHRLW(:,:,:)    ! Long-wave heating rate
    REAL(c_double), POINTER :: PVERVEL(:,:,:)  ! Vertical velocity
    REAL(c_double), POINTER :: PAP(:,:,:)      ! Pressure on full levels
    REAL(c_double), POINTER :: PLU(:,:,:)      ! Conv. condensate
    REAL(c_double), POINTER :: PLUDE(:,:,:)    ! Conv. detrained water 
    REAL(c_double), POINTER :: PSNDE(:,:,:)    ! Conv. detrained snow
    REAL(c_double), POINTER :: PMFU(:,:,:)     ! Conv. mass flux up
    REAL(c_double), POINTER :: PMFD(:,:,:)     ! Conv. mass flux down
    REAL(c_double), POINTER :: PA(:,:,:)       ! Original Cloud fraction (t)
    REAL(c_double), POINTER :: PSUPSAT(:,:,:)

    REAL(c_double), POINTER :: PLSM(:,:)       ! Land fraction (0-1) 
    LOGICAL,        POINTER :: LDCUM(:,:)      ! Convection active
    INTEGER(c_int), POINTER :: KTYPE(:,:)      ! Convection type 0,1,2
    REAL(c_double), POINTER :: PAPH(:,:,:)     ! Pressure on half levels
    REAL(c_double), POINTER :: PEXTRA(:,:,:,:) ! extra fields
    REAL(c_double), POINTER :: PCLV(:,:,:,:) 

    TYPE(STATE_TYPE), ALLOCATABLE :: TENDENCY_CML(:) ! cumulative tendency used for final output
    TYPE(STATE_TYPE), ALLOCATABLE :: TENDENCY_TMP(:) ! cumulative tendency used as input
    TYPE(STATE_TYPE), ALLOCATABLE :: TENDENCY_LOC(:) ! local tendency from cloud scheme

    ! Output fields used for validation
    !REAL(C_DOUBLE), POINTER :: PCOVPTOT(:,:,:) ! Precip fraction
    REAL(KIND=JPRB), ALLOCATABLE :: PCOVPTOT(:,:,:) ! Precip fraction
    REAL(KIND=JPRB), ALLOCATABLE :: PRAINFRAC_TOPRFZ(:,:) 
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQLF(:,:,:)   ! Flux of liquid
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQIF(:,:,:)   ! Flux of ice
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQLNG(:,:,:)  ! -ve corr for liq
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQNNG(:,:,:)  ! -ve corr for ice
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQRF(:,:,:)   ! Flux diagnostics
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQSF(:,:,:)   ! for DDH, generic
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQRNG(:,:,:)  ! rain
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQSNG(:,:,:)  ! snow
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQLTUR(:,:,:) ! liquid flux due to VDF
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQITUR(:,:,:) ! ice flux due to VDF
    REAL(KIND=JPRB), ALLOCATABLE :: PFPLSL(:,:,:)   ! liq+rain sedim flux
    REAL(KIND=JPRB), ALLOCATABLE :: PFPLSN(:,:,:)   ! ice+snow sedim flux
    REAL(KIND=JPRB), ALLOCATABLE :: PFHPSL(:,:,:)   ! Enthalpy flux for liq
    REAL(KIND=JPRB), ALLOCATABLE :: PFHPSN(:,:,:)   ! Enthalp flux for ice

    ! Underlying data buffers for AOSOA allcoated STATE_TYPE arrays
    REAL(c_double), POINTER :: B_CML(:,:,:,:)
    REAL(c_double), POINTER :: B_TMP(:,:,:,:)
!    REAL(c_double), POINTER :: B_LOC(:,:,:,:)
!    REAL(KIND=JPRB), ALLOCATABLE :: B_CML(:,:,:,:)
 !   REAL(KIND=JPRB), ALLOCATABLE :: B_TMP(:,:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: B_LOC(:,:,:,:)
  CONTAINS
    PROCEDURE :: LOAD => CLOUDSC_GLOBAL_ATLAS_STATE_LOAD
    PROCEDURE :: VALIDATE => CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE
  END TYPE CLOUDSC_GLOBAL_ATLAS_STATE

  INTERFACE FIELD_INIT
     PROCEDURE FIELD_INIT_R1, FIELD_INIT_R2, FIELD_INIT_R3, FIELD_INIT_STATE
  END INTERFACE FIELD_INIT
  INTERFACE FIELD_INIT_ATLAS
     PROCEDURE FIELD_INIT_R1_ATLAS, FIELD_INIT_R2_ATLAS, FIELD_INIT_R3_ATLAS
  END INTERFACE FIELD_INIT_ATLAS

CONTAINS

  SUBROUTINE FIELD_INIT_R1_ATLAS(FIELD, NPROMA,NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(C_DOUBLE), INTENT(INOUT) :: FIELD(:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NBLOCKS
    INTEGER(KIND=JPIM) :: B

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R1_ATLAS

  SUBROUTINE FIELD_INIT_R1(FIELD, NPROMA,NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(KIND=JPRB), ALLOCATABLE, INTENT(INOUT) :: FIELD(:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NBLOCKS
    INTEGER(KIND=JPIM) :: B
    ALLOCATE(FIELD(NPROMA, NBLOCKS))

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R1

  SUBROUTINE FIELD_INIT_R2_ATLAS(FIELD, NPROMA, NLEV, NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(C_DOUBLE), POINTER, INTENT(INOUT) :: FIELD(:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NLEV, NBLOCKS
    INTEGER(KIND=JPIM) :: B

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R2_ATLAS

  SUBROUTINE FIELD_INIT_R2(FIELD, NPROMA, NLEV, NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(KIND=JPRB), ALLOCATABLE, INTENT(INOUT) :: FIELD(:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NLEV, NBLOCKS
    INTEGER(KIND=JPIM) :: B
    ALLOCATE(FIELD(NPROMA, NLEV, NBLOCKS))

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R2

  SUBROUTINE FIELD_INIT_R3_ATLAS(FIELD, NPROMA, NLEV, NDIM, NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(C_DOUBLE), POINTER, INTENT(INOUT) :: FIELD(:,:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NLEV, NDIM, NBLOCKS
    INTEGER(KIND=JPIM) :: B

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,:,:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R3_ATLAS

  SUBROUTINE FIELD_INIT_R3(FIELD, NPROMA, NLEV, NDIM, NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(KIND=JPRB), ALLOCATABLE, INTENT(INOUT) :: FIELD(:,:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NLEV, NDIM, NBLOCKS
    INTEGER(KIND=JPIM) :: B
    ALLOCATE(FIELD(NPROMA, NLEV, NDIM, NBLOCKS))

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,:,:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R3


  SUBROUTINE FIELD_INIT_STATE(STATE, BUFFER, NPROMA, NLEV, NDIM, NBLOCKS)
    ! Allocate empty state struct array
    !
    ! Note, the resulting AOSOA pattern is a pain to roll by hand in Fortran
    ! and could be done either via parameterized derived types or templating.
    TYPE(STATE_TYPE), ALLOCATABLE, INTENT(INOUT) :: STATE(:)
    REAL(KIND=JPRB), ALLOCATABLE, TARGET, INTENT(INOUT) :: BUFFER(:,:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NLEV, NDIM, NBLOCKS
    INTEGER(KIND=JPIM) :: B, NFIELDS

    NFIELDS = 3+NDIM
    ALLOCATE(STATE(NBLOCKS))
    ALLOCATE(BUFFER(NPROMA, NLEV, NFIELDS, NBLOCKS))

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       STATE(B)%T => BUFFER(:,:,1,B)
       STATE(B)%A => BUFFER(:,:,2,B)
       STATE(B)%Q => BUFFER(:,:,3,B)
       STATE(B)%CLD => BUFFER(:,:,4:NFIELDS,B)
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_STATE

  SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_LOAD(SELF, NPROMA, NGPTOT, NGPTOTG)
    ! Load reference input data via serialbox
    CLASS(CLOUDSC_GLOBAL_ATLAS_STATE) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NGPTOT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NGPTOTG

    INTEGER(KIND=JPIM) :: KLON
    TYPE(ATLAS_FIELD) :: FIELD
    INTEGER :: IVAR

    CALL INPUT_INITIALIZE(NAME='input')

    CALL LOAD_SCALAR('KLON', KLON)
    CALL LOAD_SCALAR('KLEV', SELF%KLEV)
    CALL LOAD_SCALAR('KFLDX', SELF%KFLDX)

    GRID = ATLAS_REGULARLONLATGRID(NGPTOT, 1)
    FSPACE = ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS(GRID, LEVELS=SELF%KLEV, NPROMA=NPROMA, HALO=0)
    SELF%NBLOCKS = FSPACE%NBLKS()
    FSET = ATLAS_FIELDSET();

    DO IVAR = 1, SIZE(VAR_NAMES) - 6 ! last six variables are special
        CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME=TRIM(VAR_NAMES(IVAR)), KIND=ATLAS_REAL(JPRB)))
    ENDDO
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PLSM",   KIND=ATLAS_REAL(JPRB),    LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="LDCUM",  KIND=ATLAS_LOGICAL(),     LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="KTYPE",  KIND=ATLAS_INTEGER(JPIM), LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PAPH",   KIND=ATLAS_REAL(JPRB),    LEVELS=SELF%KLEV+1))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PEXTRA", KIND=ATLAS_REAL(JPRB),    VARIABLES=MAX(1,SELF%KFLDX)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PCLV",   KIND=ATLAS_REAL(JPRB),    VARIABLES=MAX(1,NCLV)))

    DO IVAR = 1, SIZE(SELF%VARS_3D_REAL64)
        FIELD = FSET%FIELD(TRIM(VAR_NAMES(IVAR)))
        CALL FIELD%DATA(SELF%VARS_3D_REAL64(IVAR)%PTR)
    ENDDO
    
    SELF%PLCRIT_AER => SELF%VARS_3D_REAL64(1)%PTR
    SELF%PICRIT_AER => SELF%VARS_3D_REAL64(2)%PTR
    SELF%PRE_ICE    => SELF%VARS_3D_REAL64(3)%PTR
    SELF%PCCN       => SELF%VARS_3D_REAL64(4)%PTR
    SELF%PNICE      => SELF%VARS_3D_REAL64(5)%PTR
    SELF%PT         => SELF%VARS_3D_REAL64(6)%PTR
    SELF%PQ         => SELF%VARS_3D_REAL64(7)%PTR
    SELF%PVFA       => SELF%VARS_3D_REAL64(8)%PTR
    SELF%PVFL       => SELF%VARS_3D_REAL64(9)%PTR
    SELF%PVFI       => SELF%VARS_3D_REAL64(10)%PTR
    SELF%PDYNA      => SELF%VARS_3D_REAL64(11)%PTR
    SELF%PDYNL      => SELF%VARS_3D_REAL64(12)%PTR
    SELF%PDYNI      => SELF%VARS_3D_REAL64(13)%PTR
    SELF%PHRSW      => SELF%VARS_3D_REAL64(14)%PTR
    SELF%PHRLW      => SELF%VARS_3D_REAL64(15)%PTR
    SELF%PVERVEL    => SELF%VARS_3D_REAL64(16)%PTR
    SELF%PAP        => SELF%VARS_3D_REAL64(17)%PTR
    SELF%PLU        => SELF%VARS_3D_REAL64(18)%PTR
    SELF%PLUDE      => SELF%VARS_3D_REAL64(19)%PTR
    SELF%PSNDE      => SELF%VARS_3D_REAL64(20)%PTR
    SELF%PMFU       => SELF%VARS_3D_REAL64(21)%PTR
    SELF%PMFD       => SELF%VARS_3D_REAL64(22)%PTR
    SELF%PA         => SELF%VARS_3D_REAL64(23)%PTR
    SELF%PSUPSAT    => SELF%VARS_3D_REAL64(24)%PTR

    FIELD = FSET%FIELD("PLSM")
    CALL FIELD%DATA(SELF%PLSM)
    FIELD = FSET%FIELD("LDCUM")
    CALL FIELD%DATA(SELF%LDCUM)
    FIELD = FSET%FIELD("KTYPE")
    CALL FIELD%DATA(SELF%KTYPE)
    FIELD = FSET%FIELD("PAPH")
    CALL FIELD%DATA(SELF%PAPH)
    FIELD = FSET%FIELD("PEXTRA")
    CALL FIELD%DATA(SELF%PEXTRA)
    FIELD = FSET%FIELD("PCLV")
    CALL FIELD%DATA(SELF%PCLV)

    DO IVAR = 1, SIZE(VAR_NAMES)
        CALL LOADVAR_ATLAS(FSET, TRIM(VAR_NAMES(IVAR)), KLON, NGPTOTG)
    ENDDO

    FIELD = FSPACE%CREATE_FIELD(NAME='TENDENCY_CML', KIND=ATLAS_REAL(JPRB), VARIABLES=3+NCLV) ! state is 3+ndim
    CALL FIELD%DATA(SELF%B_CML)
    CALL FSET%ADD(FIELD)
    FIELD = FSPACE%CREATE_FIELD(NAME='TENDENCY_TMP', KIND=ATLAS_REAL(JPRB), VARIABLES=3+NCLV)
    CALL FIELD%DATA(SELF%B_TMP)
    CALL FSET%ADD(FIELD)
!    FIELD = FSPACE%create_field(name='B_LOC'    , kind=atlas_real(JPRB))
!    CALL FIELD%DATA(SELF%B_LOC)
!    CALL FSTATE%add(FIELD)

    ! The STATE_TYPE arrays are tricky, as the AOSOA layout needs to be expictly
    ! unrolled at every step, and we rely on dirty hackery to do this.
    CALL FIELD_INIT(SELF%TENDENCY_LOC, SELF%B_LOC, NPROMA, SELF%KLEV, NCLV, SELF%NBLOCKS)
    CALL LOADSTATE_ATLAS(FSET, 'TENDENCY_CML', SELF%TENDENCY_CML, KLON, NGPTOTG)
    CALL LOADSTATE_ATLAS(FSET, 'TENDENCY_TMP', SELF%TENDENCY_TMP, KLON, NGPTOTG)


    ! Output fields are simply allocated and zero'd
    CALL FIELD_INIT(SELF%PRAINFRAC_TOPRFZ, NPROMA, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PCOVPTOT, NPROMA, SELF%KLEV, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFSQLF, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFSQIF, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFCQLNG, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFCQNNG, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFSQRF, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFSQSF, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFCQRNG, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFCQSNG, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFSQLTUR, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFSQITUR, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFPLSL, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFPLSN, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFHPSL, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFHPSN, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)

    ! Initialize global parameters from the input file
    CALL LOAD_SCALAR('PTSPHY', SELF%PTSPHY)
    CALL LOAD_SCALAR('LDSLPHY', SELF%LDSLPHY)
    CALL LOAD_SCALAR('LDMAINCALL', SELF%LDMAINCALL)
    CALL YOMCST_LOAD_PARAMETERS()
    CALL YOETHF_LOAD_PARAMETERS()
    CALL YRECLDP_LOAD_PARAMETERS()
    CALL YREPHLI_LOAD_PARAMETERS()

    CALL INPUT_FINALIZE()

  END SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_LOAD

  SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE(SELF, NPROMA, NGPTOT, NGPTOTG)
    ! Validate the correctness of output against reference data
    CLASS(CLOUDSC_GLOBAL_ATLAS_STATE) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NGPTOT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NGPTOTG

    REAL(KIND=JPRB), ALLOCATABLE :: PLUDE(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PCOVPTOT(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PRAINFRAC_TOPRFZ(:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQLF(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQIF(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQLNG(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQNNG(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQRF(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQSF(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQRNG(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQSNG(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQLTUR(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQITUR(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFPLSL(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFPLSN(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFHPSL(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFHPSN(:,:,:)
    TYPE(STATE_TYPE), ALLOCATABLE :: TENDENCY_LOC(:)
    REAL(KIND=JPRB), ALLOCATABLE :: B_LOC(:,:,:,:)

    INTEGER(KIND=JPIM) :: NBLOCKS, KLON, KLEV, KFLDX

    CALL INPUT_INITIALIZE(NAME='reference')

    NBLOCKS = (NGPTOT / NPROMA) + MIN(MOD(NGPTOT,NPROMA), 1)
    CALL LOAD_SCALAR('KLON', KLON)
    CALL LOAD_SCALAR('KLEV', KLEV)
    CALL LOAD_SCALAR('KFLDX', KFLDX)

    ! Write variable validation header
    IF (IRANK == 0) THEN
      print '(1X,A20,1X,A3,5(1X,A20))', &
           & 'Variable','Dim', 'MinValue','MaxValue','AbsMaxErr','AvgAbsErr/GP','MaxRelErr-%'
    END IF

    ! Load reference data and expand to our block layout
    CALL LOAD_AND_EXPAND('PLUDE', PLUDE, KLON, SELF%KLEV, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PCOVPTOT', PCOVPTOT, KLON, SELF%KLEV, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PRAINFRAC_TOPRFZ', PRAINFRAC_TOPRFZ, KLON, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFSQLF', PFSQLF, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFSQIF', PFSQIF, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFCQLNG', PFCQLNG, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFCQNNG', PFCQNNG, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFSQRF', PFSQRF, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFSQSF', PFSQSF, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFCQRNG', PFCQRNG, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFCQSNG', PFCQSNG, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFSQLTUR', PFSQLTUR, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFSQITUR', PFSQITUR, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFPLSL', PFPLSL, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFPLSN', PFPLSN, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFHPSL', PFHPSL, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFHPSN', PFHPSN, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND_STATE('TENDENCY_LOC', TENDENCY_LOC, B_LOC, KLON, SELF%KLEV, NCLV, &
         & NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL INPUT_FINALIZE()

    ! Actual variable validation
    CALL VALIDATE('PLUDE', PLUDE, SELF%PLUDE, NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PCOVPTOT', PCOVPTOT, SELF%PCOVPTOT, NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PRAINFRAC_TOPRFZ', PRAINFRAC_TOPRFZ, SELF%PRAINFRAC_TOPRFZ, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFSQLF', PFSQLF, SELF%PFSQLF, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFSQIF', PFSQIF, SELF%PFSQIF, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFCQLNG', PFCQLNG, SELF%PFCQLNG, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFCQNNG', PFCQNNG, SELF%PFCQNNG, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFSQRF', PFSQRF, SELF%PFSQRF, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFSQSF', PFSQSF, SELF%PFSQSF, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFCQRNG', PFCQRNG, SELF%PFCQRNG, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFCQSNG', PFCQSNG, SELF%PFCQSNG, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFSQLTUR', PFSQLTUR, SELF%PFSQLTUR, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFSQITUR', PFSQITUR, SELF%PFSQITUR, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFPLSL', PFPLSL, SELF%PFPLSL, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFPLSN', PFPLSN, SELF%PFPLSN, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFHPSL', PFHPSL, SELF%PFHPSL, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFHPSN', PFHPSN, SELF%PFHPSN, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    ! This is extremly hacky, but the array-of-struct-of-array layout demands it.
    CALL VALIDATE('TENDENCY_LOC%A', B_LOC(:,:,2,:), SELF%B_LOC(:,:,2,:), NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('TENDENCY_LOC%Q', B_LOC(:,:,3,:), SELF%B_LOC(:,:,3,:), NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('TENDENCY_LOC%T', B_LOC(:,:,1,:), SELF%B_LOC(:,:,1,:), NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('TENDENCY_LOC%CLD', B_LOC(:,:,4:,:), SELF%B_LOC(:,:,4:,:), NPROMA, SELF%KLEV, NCLV, NGPTOT, NBLOCKS, NGPTOTG)

  END SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE

END MODULE CLOUDSC_GLOBAL_ATLAS_STATE_MOD
